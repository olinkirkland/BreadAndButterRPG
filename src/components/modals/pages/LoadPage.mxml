<?xml version="1.0"?>
<pages:Page xmlns:fx="http://ns.adobe.com/mxml/2009"
            xmlns:s="library://ns.adobe.com/flex/spark"
            xmlns:pages="components.modals.pages.*">

    <fx:Script>
        <![CDATA[
        import logic.Save;
        import logic.SortUtil;
        import logic.managers.PageManager;
        import logic.managers.SaveManager;

        import mx.collections.ArrayCollection;

        import spark.events.IndexChangeEvent;

        private var saveManager:SaveManager;

        override public function show():void
        {
            super.show();

            saveManager = SaveManager.instance;
            saveManager.addEventListener(SaveManager.GAME_LOADED, onGameLoaded);

            validateCharacters();
        }

        private function validateCharacters():void
        {
            if (dropDownListCharacterNames.dataProvider)
                dropDownListCharacterNames.removeEventListener(IndexChangeEvent.CHANGE, onChangeCharacterName);

            var characters:Array = [];
            var characterFolders:Array = File.applicationStorageDirectory.resolvePath("saves").getDirectoryListing();
            for each (var f:File in characterFolders)
                characters.push(f.name);
            dropDownListCharacterNames.dataProvider = new ArrayCollection(characters);
            dropDownListCharacterNames.addEventListener(IndexChangeEvent.CHANGE, onChangeCharacterName);

            // If there are no characters, go back to the start screen
            if (characters.length == 0)
            {
                PageManager.open(new StartPage());
                return;
            }

            callLater(validateSaves);
        }

        private function validateSaves():void
        {
            saveManager.getSaves(dropDownListCharacterNames.selectedItem, onGetSaves);
        }

        private function onChangeCharacterName(event:IndexChangeEvent):void
        {
            // Load save list
            saveManager.getSaves(dropDownListCharacterNames.selectedItem, onGetSaves);
        }

        private function onGetSaves(saves:Array):void
        {
            var arr:ArrayCollection = new ArrayCollection(saves);
            arr.sort = SortUtil.byTime;
            arr.refresh();
            listSaves.dataProvider = arr;

            // Delete the character if there are no saves
            if (arr.length == 0)
            {
                var f:File = File.applicationStorageDirectory.resolvePath("saves/" + dropDownListCharacterNames.selectedItem);
                f.deleteDirectory(true);
                validateCharacters();
            }
        }

        private function onGameLoaded(event:Event):void
        {
            PageManager.open(new GamePage());
        }

        private function onClickLoad(event:MouseEvent):void
        {
            // Load the selected save
            var save:Save = listSaves.selectedItem;
            SaveManager.instance.loadGame(save.data);
        }

        private function onClickDelete(event:MouseEvent):void
        {
            // Delete the selected save
            var file:File = listSaves.selectedItem.file;
            file.deleteFile();
            validateSaves();
        }

        private function onClickCancel(event:MouseEvent):void
        {
            PageManager.open(new StartPage());
        }
        ]]>
    </fx:Script>

    <s:Group width="100%"
             height="100%">

        <s:BitmapImage id="bitmapImage"
                       smooth="true"
                       smoothingQuality="high"/>

        <s:VGroup id="content"
                  width="100%"
                  height="100%"
                  gap="0">

            <s:Panel width="100%"
                     height="100%">
                <s:VGroup width="100%"
                          height="100%"
                          horizontalAlign="center"
                          verticalAlign="middle">
                    <s:VGroup width="300"
                              height="100%"
                              horizontalAlign="center"
                              verticalAlign="middle">
                        <s:Label text="load"/>
                        <s:DropDownList id="dropDownListCharacterNames"
                                        requireSelection="true"
                                        width="100%"/>
                        <s:List id="listSaves"
                                width="100%"
                                height="200"
                                requireSelection="true"
                                labelField="name"/>
                        <s:Button label="load game"
                                  click="onClickLoad(event)"/>
                        <s:HGroup>
                            <s:Button label="delete save"
                                      click="onClickDelete(event)"/>
                            <s:Button label="cancel"
                                      click="onClickCancel(event)"/>
                        </s:HGroup>
                    </s:VGroup>
                </s:VGroup>
            </s:Panel>

        </s:VGroup>
    </s:Group>

</pages:Page>