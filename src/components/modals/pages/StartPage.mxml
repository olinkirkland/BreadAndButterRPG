<?xml version="1.0"?>
<pages:Page xmlns:fx="http://ns.adobe.com/mxml/2009"
            xmlns:s="library://ns.adobe.com/flex/spark"
            xmlns:pages="components.modals.pages.*">

    <fx:Script>
        <![CDATA[
        import global.SharedProperty;
        import global.Utils;

        import logic.Game;
        import logic.Save;
        import logic.SortUtil;
        import logic.managers.PageManager;
        import logic.managers.SaveManager;

        import mx.collections.ArrayCollection;

        private var saveManager:SaveManager;
        private var mostRecentSave:Save;

        override public function show():void
        {
            super.show();

            saveManager = SaveManager.instance;
            saveManager.addEventListener(SaveManager.GAME_LOADED, onGameLoaded);

            var f:File = File.applicationStorageDirectory.resolvePath("saves");
            buttonLoad.enabled = f.exists && f.getDirectoryListing().length > 0;

            // Do some start stuff
            Utils.initSharedObject();

            // Get the most recent character/save
            var hasLastPlayedCharacterName:Boolean = Utils.getProperty(SharedProperty.LAST_PLAYED_AS_CHARACTER);
            var lastPlayedCharacterName:String = String(Utils.getProperty(SharedProperty.LAST_PLAYED_AS_CHARACTER));
            if (hasLastPlayedCharacterName)
            {
                var d:File = File.applicationStorageDirectory.resolvePath("saves/" + lastPlayedCharacterName);
                if (d.exists)
                    SaveManager.instance.getSaves(lastPlayedCharacterName, onGetSaves);
                else
                    lastPlayedCharacterName = Utils.setProperty(SharedProperty.LAST_PLAYED_AS_CHARACTER, null);
            }

            hasLastPlayedCharacterName = Utils.getProperty(SharedProperty.LAST_PLAYED_AS_CHARACTER);

            labelRecentSave.text = lastPlayedCharacterName;
            panelContinue.visible = panelContinue.includeInLayout = hasLastPlayedCharacterName;
        }

        private function onGetSaves(saves:Array):void
        {
            if (saves.length == 0)
            {
                mostRecentSave = null;
                panelContinue.visible = panelContinue.includeInLayout = false;
                return;
            }

            var arr:ArrayCollection = new ArrayCollection(saves);
            arr.sort = SortUtil.byTime;
            arr.refresh();
            mostRecentSave = Save(arr.getItemAt(0));
            labelRecentTime.text = mostRecentSave.name + " ... " + Utils.toRelativeDate(mostRecentSave.time);
        }

        private function onGameLoaded(event:Event):void
        {
            var gameState:Game = Game.instance;

            PageManager.open(gameState.characterName ? new GamePage() : new CharacterCreatePage());
        }

        private function onClickNew(event:MouseEvent):void
        {
            saveManager.loadNewGame();
        }

        private function onClickLoad(event:MouseEvent):void
        {
            PageManager.open(new LoadPage());
        }

        private function onClickExit(event:MouseEvent):void
        {
            NativeApplication.nativeApplication.exit();
        }

        private function onClickContinue(event:MouseEvent):void
        {
            var lastPlayedCharacterName:String = String(Utils.getProperty(SharedProperty.LAST_PLAYED_AS_CHARACTER));
            if (!lastPlayedCharacterName)
                return;

            // Load most recent save
            SaveManager.instance.loadGame(mostRecentSave.data);
        }
        ]]>
    </fx:Script>

    <s:Group width="100%"
             height="100%">

        <s:BitmapImage id="bitmapImage"
                       smooth="true"
                       smoothingQuality="high"/>

        <s:VGroup id="content"
                  width="100%"
                  height="100%"
                  gap="0">

            <s:Panel width="100%"
                     height="100%">
                <s:VGroup width="100%"
                          height="100%"
                          horizontalAlign="center"
                          verticalAlign="middle">
                    <s:Label text="start"/>

                    <s:Panel id="panelContinue">
                        <s:VGroup horizontalAlign="center">
                            <s:Button id="buttonContinue"
                                      label="continue"
                                      click="onClickContinue(event)"/>
                            <s:Label id="labelRecentSave"
                                     text="..."
                                     paddingTop="5"
                                     alpha=".6"/>
                            <s:Label id="labelRecentTime"
                                     alpha=".6"
                                     fontStyle="italic"/>
                        </s:VGroup>
                    </s:Panel>

                    <s:Button label="new"
                              click="onClickNew(event)"/>

                    <s:Button id="buttonLoad"
                              label="load"
                              click="onClickLoad(event)"/>
                    <s:Button label="exit"
                              click="onClickExit(event)"/>
                </s:VGroup>
            </s:Panel>

        </s:VGroup>
    </s:Group>

</pages:Page>